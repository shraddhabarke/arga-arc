library: "(" program* ")"

// Rules are executed one after another, in the order they appear.
// There could be no rules, in which case the program does nothing.
program: "(" "do" rule* ")"

// First, it defines a filter expression that an object must satisfy in order to be transformed.
// This filter expression can also refer to one additional object, which is not transformed,
// but whose properties can be used by the transform expressions.
// Then, it defines a list of transforms that will be applied objects that satisfy the filter expression.
rule: "(" "rule" filter xform_list ")"

// `var` referst to the additional object, which might or might not be present.
VAR: "var"

// A filter is a predicate over the objects in the grid.
// If no filter is given, then all the objects are selected.
filter: "(" "filter" filter_expr? ")"

// It can be a primitive filter operation, or a boolean expression using the `and`, `or`, and `not` connectives
// and the `varand` connective, which is used to constrain the optional additional object.
// The objects that are transformed are the ones that satisfy the filter expression.
filter_expr: filter_prim
           | "(" OP_AND filter_expr filter_expr ")"   
           | "(" OP_OR filter_expr filter_expr ")"
           | "(" OP_NOT filter_expr ")"
           | "(" OP_VARAND filter_relation filter_expr ")"

filter_prim: "(" FL_BY_COLOR COLOR ")"           // select objects of the given color
           | "(" FL_BY_SIZE SIZE ")"             // select the objects that have the given size
           | "(" FL_BY_HEIGHT HEIGHT ")"         // select all objects that have a given height
           | "(" FL_BY_WIDTH WIDTH ")"           // select all objects that have a given width
           | "(" FL_BY_DEGREE DEGREE ")"         // select all objects that have a given number of neighbors i.e degree
           | "(" FL_BY_SHAPE SHAPE ")"           // select all objects that have the given shape
           | "(" FL_BY_COLUMNS COLUMN ")"        // select all objects that are in the given column
           | "(" FL_BY_NEIGHBOR_SIZE SIZE ")"    // select all objects that have a neighbor of the given size
           | "(" FL_BY_NEIGHBOR_COLOR COLOR ")"  // select all objects that have a neighbor of the given color
           | "(" FL_BY_NEIGHBOR_DEGREE DEGREE ")"// select all objects that have a neighbor of the given degree

filter_relation: "(" REL_ANY_NEIGHBOR ")"      // selects the auxiliary object if it is a neighbor of the main object, in any direction
               | "(" REL_DIRECT_NEIGHBOR ")"   // selects the auxiliary object if it is a neighbor of the main object, horizontally or vertically
               | "(" REL_DIAGONAL_NEIGHBOR ")" // selects the auxiliary object if it is a neighbor of the main object, diagonally

// Transforms are applied one after another, in the order they appear.
xform_list: "(" "apply" xform+ ")"

// A single transform is applied to all the objects that are bound to the `this` variable. 
// The transform is applied to each such object independently and simultaneously.
xform: "(" TR_UPDATE_COLOR COLOR ")"                          // change this object color to the given color 
         | "(" TR_UPDATE_COLOR VAR ")"                            // or the color of the given object
         | "(" TR_MOVE_NODE DIRECTION ")"                         // move this object in the given direction
         | "(" TR_MOVE_NODE VAR ")"                               // or the direction of the given object
         | "(" TR_EXTEND_NODE DIRECTION OVERLAP ")"               // extend (move while leaving a trail) this object in the given direction
         | "(" TR_EXTEND_NODE VAR OVERLAP ")"                     // or the direction of the given object 
                                                                  // if overlap is false, then the object is extended until it hits another object,
                                                                  // otherwise it is extended until the edge of the grid.
         | "(" TR_MOVE_NODE_MAX DIRECTION ")"                     // move this object until it hits another object, in the given direction
         | "(" TR_MOVE_NODE_MAX VAR ")"                           // or this direction of the given object
         | "(" TR_ROTATE_NODE ROT_ANGLE ")"                       // rotate this object by the given angle
         | "(" TR_ADD_BORDER COLOR ")"                            // color this exterior pixels of the object with the given color
         | "(" TR_FILL_RECTANGLE COLOR OVERLAP ")"                // color this interior of a hollow rectangle with the given color
         | "(" TR_HOLLOW_RECTANGLE COLOR ")"                      // color this interior of a solid rectangle with the given color
         | "(" TR_MIRROR VAR ")"                                  // mirror this object with respect to the given object
         | "(" TR_FLIP SYMMETRY_AXIS ")"                          // flip this object with respect to the given axis
         | "(" TR_INSERT VAR IMAGE_POINTS RELATIVE_POSITION ")"   // insert a copy of this object at the position of the given object
         | "(" TR_NOOP ")"                                        // do nothing

SIZE: "min" | "max" | "odd" | /[0-9]+/
DEGREE: "min" | "max" | "odd" | /[0-9]+/
HEIGHT: "min" | "max" | "odd" | /[0-9]+/
WIDTH: "min" | "max" | "odd" | /[0-9]+/
COLUMN: "center" | "even" | /[0-9]+/
SHAPE: "square" | "enclosed"

DIRECTION: "U"
         | "D"
         | "L"
         | "R"
         | "UL"
         | "UR"
         | "DL"
         | "DR"

SYMMETRY_AXIS: "vertical" | "horizontal" | "diagonal_left" | "diagonal_right"

// The angle of clockwise rotation, in degrees.
ROT_ANGLE: "0" 
         | "90" 
         | "180" 
         | "270"

COLOR: "O" // black
      | "B" // blue
      | "R" // red
      | "G" // green
      | "Y" // yellow
      | "X" // grey
      | "F" // fuchsia
      | "A" // orange
      | "C" // cyan  
      | "W" // brown

OVERLAP: BOOL

BOOL: "true" | "false"

IMAGE_POINTS: "top"
            | "bottom"
            | "left"
            | "right"
            | "top_left"
            | "top_right"
            | "bottom_left"
            | "bottom_right"

RELATIVE_POSITION: "source"
                 | "target"
                 | "middle"

OP_AND: "and"
OP_OR: "or"
OP_NOT: "not"
OP_VARAND: "varand"

FL_BY_COLOR: "filter_by_color" 
FL_BY_SIZE: "filter_by_size"
FL_BY_DEGREE: "filter_by_degree"
FL_BY_HEIGHT: "filter_by_height"
FL_BY_WIDTH: "filter_by_width"
FL_BY_SHAPE: "filter_by_shape"
FL_BY_COLUMNS: "filter_by_columns"
FL_BY_NEIGHBOR_SIZE: "filter_by_neighbor_size"
FL_BY_NEIGHBOR_COLOR: "filter_by_neighbor_color"
FL_BY_NEIGHBOR_DEGREE: "filter_by_neighbor_degree"

REL_ANY_NEIGHBOR: "is_any_neighbor"
REL_DIRECT_NEIGHBOR: "is_direct_neighbor"
REL_DIAGONAL_NEIGHBOR: "is_diagonal_neighbor"

TR_UPDATE_COLOR: "update_color"
TR_MOVE_NODE: "move_node"
TR_EXTEND_NODE: "extend_node"
TR_MOVE_NODE_MAX: "move_node_max"
TR_ROTATE_NODE: "rotate_node"
TR_ADD_BORDER: "add_border"
TR_FILL_RECTANGLE: "fill_rectangle"
TR_HOLLOW_RECTANGLE: "hollow_rectangle"
TR_MIRROR: "mirror"
TR_FLIP: "flip"
TR_INSERT: "insert"
TR_NOOP: "noop"

COMMENT: /\s*/ ";;" /[^\n]/*

%import common.WS
%import common.LCASE_LETTER
%import common.INT
%ignore WS
%ignore COMMENT